apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven'

version '0.1.0'
group 'com.github.reiseburo'
description 'A reactive wrapper around Apache Curator'
defaultTasks 'check', 'assemble'

repositories {
    jcenter()
}

dependencies {
    ['framework', 'recipes'].each {
        compile "org.apache.curator:curator-${it}:[2.7.1,2.8)"
    }
    compile 'io.reactivex:rxjava:[1.0.14,2.0)'

    testCompile 'org.apache.curator:curator-test:[2.7.1,2.8)'
    testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
    testCompile 'cglib:cglib-nodep:3.1'
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

test {
    testLogging {
        exceptionFormat = 'full'
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    dependsOn classes
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    dependsOn javadoc
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}


assemble.dependsOn check, javadocJar, sourcesJar
install.dependsOn assemble



plugins.withType(JavaPlugin) {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7


    project.tasks.withType(JavaCompile) { task ->
        task.sourceCompatibility = project.sourceCompatibility
        task.targetCompatibility = project.targetCompatibility
    }

    project.tasks.withType(GroovyCompile) { task ->
        task.sourceCompatibility = project.sourceCompatibility
        task.targetCompatibility = project.targetCompatibility
    }
}
